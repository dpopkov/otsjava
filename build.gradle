plugins {
    // support Idea IDE
    id 'idea'
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
    // Build uber-jar
    id 'com.github.johnrengelman.shadow' version '5.2.0' apply false
}

idea {
    project {
        languageLevel = 11
    }
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

allprojects {
    group "learn.otsjava"

    repositories {
        // Use JCenter for resolving dependencies.
        jcenter()
    }

    apply plugin: "io.spring.dependency-management"
    dependencyManagement {
        dependencies {
            imports {
                mavenBom("org.springframework.boot:spring-boot-dependencies:${Versions.springBootDependencies}")
            }
            dependency("com.google.guava:guava:${Versions.guava}")
            dependency("org.openjdk.jmh:jmh-core:${Versions.jmh}")
            dependency("org.openjdk.jmh:jmh-generator-annprocess:${Versions.jmh}")
        }
    }

    configurations.all {
        // fail eagerly on version conflict (includes transitive dependencies)
        // e.g multiple different versions of the same dependency (group and name are equal)
        resolutionStrategy {
            failOnVersionConflict()
        }
    }
}

// Print versions from BOM
task managedVersions {
    doLast {
        dependencyManagement.managedVersions.each {
            println it
        }
    }
}
